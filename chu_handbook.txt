model{
  for(i in 1:ns){

  logit(Se1[i]) <- l[i,1]
  logit(Sp1[i]) <- l[i,2]
  logit(Sp2[i]) <- l[i,3]
  logit(pi[i]) <- l[i,4]

  prob[i,1]<-pi[i]*Se1[i]*Se2+(1-pi[i])*(1-Sp1[i])*(1-Sp2[i])
  prob[i,2]<-pi[i]*Se1[i]*(1-Se2)+(1-pi[i])*(1-Sp1[i])*Sp2[i]
  prob[i,3]<-pi[i]*(1-Se1[i])*Se2+(1-pi[i])*Sp1[i]*(1-Sp2[i])
  prob[i,4]<-pi[i]*(1-Se1[i])*(1-Se2)+(1-pi[i])*Sp1[i]*Sp2[i]


  r[i,1:4]~dmulti(prob[i,1:4],n[i])

  # joint distribution for pap test sens1 & spec1
  l[i,1:2] ~ dmnorm(mu[1:2], T[,])

  # distribution for spec2
  l[i,3] ~ dnorm(mu[3], sigma[3])

  # distribution for prevalence
  l[i,4] ~ dnorm(mu[4], sigma[4])
  }

  mu[1] ~ dnorm(0,0.25)
  mu[2] ~ dnorm(0,0.25)
  mu[3] ~ dnorm(0,0.25)
  mu[4] ~ dnorm(0,0.25)

  T[1:2,1:2]<-inverse(S[,])

  S[1,1] <-sigma[1] * sigma[1]
  S[2,2] <-sigma[2] * sigma[2]

  S[1,2] <- rho * sigma[1] * sigma[2]
  S[2,1] <- rho * sigma[2] * sigma[1]

  prec[1] ~ dgamma(2, 0.5)
  prec[2] ~ dgamma(2, 0.5)
  prec[3] ~ dgamma(2, 0.5)
  prec[4] ~ dgamma(2, 0.5)
  sigma[1] <- pow(prec[1], -0.5)
  sigma[2] <- pow(prec[2], -0.5)
  sigma[3] <- pow(prec[3], -0.5)
  sigma[4] <- pow(prec[4], -0.5)

  # assume common correlation
  rho ~ dunif(-1, 1)

  # Sensitivity
  sens1 <- 1/(1+exp(-mu[1]))
  sens2 <- Se2
  Se2 ~ dunif(0,1)

  # Specificity: mu[2] is logit FPR
  spec1 <- 1/(1+exp(-mu[2]))
  spec2 <- 1/(1+exp(-mu[3]))

  # Prevalence
  prev <- 1/(1+exp(-mu[4]))

  bsens1 ~ dbern(gamma.sens1)
  gamma.sens1 <- step(sens2 - sens1) - equals(sens2,sens1)

  bspec1 ~ dbern(gamma.spec1)
  gamma.spec1 <- step(spec2 - spec1) - equals(spec2,spec1)
}
