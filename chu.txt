model{
  for(i in 1:ns){

  logit(Se1[i]) <- l[i,1]
  logit(Sp1[i]) <- l[i,2]

  prob[i,1]<-pi[i]*Se1[i]*Se2+(1-pi[i])*(1-Sp1[i])*(1-Sp2)
  prob[i,2]<-pi[i]*Se1[i]*(1-Se2)+(1-pi[i])*(1-Sp1[i])*Sp2
  prob[i,3]<-pi[i]*(1-Se1[i])*Se2+(1-pi[i])*Sp1[i]*(1-Sp2)
  prob[i,4]<-pi[i]*(1-Se1[i])*(1-Se2)+(1-pi[i])*Sp1[i]*Sp2


  r[i,1:4]~dmulti(prob[i,1:4],n[i])
  pi[i]~dbeta(1,1)

  # joint distribution for pap test sens1 & spec1
  l[i,1:2] ~ dmnorm(mu[], T[,])
  }

  mu[1] ~ dnorm(0,0.25)
  mu[2]~ dnorm(0,0.25)

  T[1:2,1:2]<-inverse(S[,])

  S[1,1] <-sigma[1] * sigma[1]
  S[2,2] <-sigma[2] * sigma[2]

  S[1,2] <- rho * sigma[1] * sigma[2]
  S[2,1] <- rho * sigma[2] * sigma[1]

  prec[1] ~ dgamma(2, 0.5)
  prec[2] ~ dgamma(2, 0.5)
  sigma[1] <- pow(prec[1], -0.5)
  sigma[2] <- pow(prec[2], -0.5)

  # assume common correlation
  rho ~ dunif(-1, 1)


  # Sensitivity
  sens1 <- 1/(1+exp(-mu[1]))
  sens2 <- Se2
  Se2 ~ dunif(0,1)

  # Specificity: mu[2] is logit FPR
  spec1 <- 1/(1+exp(-mu[2]))
  spec2 <- Sp2
  Sp2 ~ dunif(0,1)

  bsens1 ~ dbern(gamma.sens1)
  gamma.sens1 <- step(sens2 - sens1) - equals(sens2,sens1)

  bspec1 ~ dbern(gamma.spec1)
  gamma.spec1 <- step(spec2 - spec1) - equals(spec2,spec1)
}
